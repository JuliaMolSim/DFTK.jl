{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Polarizability using automatic differentiation\n",
    "\n",
    "Simple example for computing properties using (forward-mode)\n",
    "automatic differentiation.\n",
    "For a more classical approach and more details about computing polarizabilities,\n",
    "see Polarizability by linear response."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using DFTK\n",
    "using LinearAlgebra\n",
    "using ForwardDiff\n",
    "\n",
    "# Construct PlaneWaveBasis given a particular electric field strength\n",
    "# Again we take the example of a Helium atom.\n",
    "function make_basis(ε::T; a=10., Ecut=30) where {T}\n",
    "    lattice=T(a) * I(3)  # lattice is a cube of $a$ Bohrs\n",
    "    # Helium at the center of the box\n",
    "    atoms     = [ElementPsp(:He; psp=load_psp(\"hgh/lda/He-q2\"))]\n",
    "    positions = [[1/2, 1/2, 1/2]]\n",
    "\n",
    "    model = model_DFT(lattice, atoms, positions, [:lda_x, :lda_c_vwn];\n",
    "                      extra_terms=[ExternalFromReal(r -> -ε * (r[1] - a/2))],\n",
    "                      symmetries=false)\n",
    "    PlaneWaveBasis(model; Ecut, kgrid=[1, 1, 1])  # No k-point sampling on isolated system\n",
    "end\n",
    "\n",
    "# dipole moment of a given density (assuming the current geometry)\n",
    "function dipole(basis, ρ)\n",
    "    @assert isdiag(basis.model.lattice)\n",
    "    a  = basis.model.lattice[1, 1]\n",
    "    rr = [a * (r[1] - 1/2) for r in r_vectors(basis)]\n",
    "    sum(rr .* ρ) * basis.dvol\n",
    "end\n",
    "\n",
    "# Function to compute the dipole for a given field strength\n",
    "function compute_dipole(ε; tol=1e-8, kwargs...)\n",
    "    scfres = self_consistent_field(make_basis(ε; kwargs...); tol)\n",
    "    dipole(scfres.basis, scfres.ρ)\n",
    "end;"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "With this in place we can compute the polarizability from finite differences\n",
    "(just like in the previous example):"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime\n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -2.770920312102                   -0.52    9.0         \n",
      "  2   -2.772150183437       -2.91       -1.32    1.0    104ms\n",
      "  3   -2.772170286328       -4.70       -2.47    1.0    139ms\n",
      "  4   -2.772170656367       -6.43       -3.20    1.0    107ms\n",
      "  5   -2.772170722791       -7.18       -4.27    2.0    124ms\n",
      "  6   -2.772170722867      -10.12       -4.39    1.0    140ms\n",
      "  7   -2.772170723013       -9.84       -5.33    1.0    113ms\n",
      "  8   -2.772170723014      -11.81       -5.49    2.0    128ms\n",
      "  9   -2.772170723015      -12.07       -6.19    1.0    125ms\n",
      " 10   -2.772170723015      -12.98       -6.57    1.0    126ms\n",
      " 11   -2.772170723015   +  -13.75       -6.91    1.0    128ms\n",
      " 12   -2.772170723015      -13.75       -7.95    1.0    129ms\n",
      " 13   -2.772170723015   +  -14.35       -7.87    2.0    149ms\n",
      " 14   -2.772170723015   +  -14.40       -8.36    1.0    120ms\n",
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime\n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -2.770679344879                   -0.53    8.0         \n",
      "  2   -2.772049262071       -2.86       -1.31    1.0    133ms\n",
      "  3   -2.772082583711       -4.48       -2.62    1.0    105ms\n",
      "  4   -2.772083415145       -6.08       -3.98    2.0    120ms\n",
      "  5   -2.772083417702       -8.59       -4.51    2.0    156ms\n",
      "  6   -2.772083417805       -9.99       -5.33    1.0    110ms\n",
      "  7   -2.772083417811      -11.26       -6.24    2.0    126ms\n",
      "  8   -2.772083417811      -13.76       -6.95    1.0    125ms\n",
      "  9   -2.772083417811      -13.88       -7.53    2.0    139ms\n",
      " 10   -2.772083417811   +  -14.05       -8.25    1.0    128ms\n"
     ]
    },
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "1.7735580312637835"
     },
     "metadata": {},
     "execution_count": 2
    }
   ],
   "cell_type": "code",
   "source": [
    "polarizability_fd = let\n",
    "    ε = 0.01\n",
    "    (compute_dipole(ε) - compute_dipole(0.0)) / ε\n",
    "end"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "We do the same thing using automatic differentiation. Under the hood this uses\n",
    "custom rules to implicitly differentiate through the self-consistent\n",
    "field fixed-point problem."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime\n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -2.770735590979                   -0.53    9.0         \n",
      "  2   -2.772049320186       -2.88       -1.31    1.0    104ms\n",
      "  3   -2.772082507388       -4.48       -2.59    1.0    105ms\n",
      "  4   -2.772083411625       -6.04       -3.78    2.0    162ms\n",
      "  5   -2.772083417014       -8.27       -4.08    2.0    123ms\n",
      "  6   -2.772083417789       -9.11       -5.23    1.0    111ms\n",
      "  7   -2.772083417810      -10.67       -5.70    2.0    143ms\n",
      "  8   -2.772083417811      -12.49       -6.29    1.0    114ms\n",
      "  9   -2.772083417811      -13.09       -7.28    2.0    148ms\n",
      " 10   -2.772083417811   +  -14.88       -8.09    2.0    144ms\n",
      "\n",
      "Polarizability via ForwardDiff:       1.7725349480420538\n",
      "Polarizability via finite difference: 1.7735580312637835\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "polarizability = ForwardDiff.derivative(compute_dipole, 0.0)\n",
    "println()\n",
    "println(\"Polarizability via ForwardDiff:       $polarizability\")\n",
    "println(\"Polarizability via finite difference: $polarizability_fd\")"
   ],
   "metadata": {},
   "execution_count": 3
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.4"
  },
  "kernelspec": {
   "name": "julia-1.9",
   "display_name": "Julia 1.9.4",
   "language": "julia"
  }
 },
 "nbformat": 4
}
