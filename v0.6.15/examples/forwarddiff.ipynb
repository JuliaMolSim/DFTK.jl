{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Polarizability using automatic differentiation\n",
    "\n",
    "Simple example for computing properties using (forward-mode)\n",
    "automatic differentiation.\n",
    "For a more classical approach and more details about computing polarizabilities,\n",
    "see Polarizability by linear response."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using DFTK\n",
    "using LinearAlgebra\n",
    "using ForwardDiff\n",
    "\n",
    "# Construct PlaneWaveBasis given a particular electric field strength\n",
    "# Again we take the example of a Helium atom.\n",
    "function make_basis(ε::T; a=10., Ecut=30) where {T}\n",
    "    lattice=T(a) * I(3)  # lattice is a cube of $a$ Bohrs\n",
    "    # Helium at the center of the box\n",
    "    atoms     = [ElementPsp(:He; psp=load_psp(\"hgh/lda/He-q2\"))]\n",
    "    positions = [[1/2, 1/2, 1/2]]\n",
    "\n",
    "    model = model_DFT(lattice, atoms, positions, [:lda_x, :lda_c_vwn];\n",
    "                      extra_terms=[ExternalFromReal(r -> -ε * (r[1] - a/2))],\n",
    "                      symmetries=false)\n",
    "    PlaneWaveBasis(model; Ecut, kgrid=[1, 1, 1])  # No k-point sampling on isolated system\n",
    "end\n",
    "\n",
    "# dipole moment of a given density (assuming the current geometry)\n",
    "function dipole(basis, ρ)\n",
    "    @assert isdiag(basis.model.lattice)\n",
    "    a  = basis.model.lattice[1, 1]\n",
    "    rr = [a * (r[1] - 1/2) for r in r_vectors(basis)]\n",
    "    sum(rr .* ρ) * basis.dvol\n",
    "end\n",
    "\n",
    "# Function to compute the dipole for a given field strength\n",
    "function compute_dipole(ε; tol=1e-8, kwargs...)\n",
    "    scfres = self_consistent_field(make_basis(ε; kwargs...); tol)\n",
    "    dipole(scfres.basis, scfres.ρ)\n",
    "end;"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "With this in place we can compute the polarizability from finite differences\n",
    "(just like in the previous example):"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime\n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -2.770723729957                   -0.53    9.0         \n",
      "  2   -2.772139017895       -2.85       -1.30    1.0    115ms\n",
      "  3   -2.772170168083       -4.51       -2.63    1.0    137ms\n",
      "  4   -2.772170721793       -6.26       -4.02    2.0    123ms\n",
      "  5   -2.772170722984       -8.92       -4.76    2.0    147ms\n",
      "  6   -2.772170723014      -10.54       -5.75    1.0    111ms\n",
      "  7   -2.772170723015      -11.84       -6.21    2.0    128ms\n",
      "  8   -2.772170723015      -13.35       -7.10    1.0    130ms\n",
      "  9   -2.772170723015   +  -14.03       -8.19    1.0    116ms\n",
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime\n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -2.770708846978                   -0.53    8.0         \n",
      "  2   -2.772050698551       -2.87       -1.31    1.0    106ms\n",
      "  3   -2.772082837847       -4.49       -2.61    1.0    124ms\n",
      "  4   -2.772083414190       -6.24       -3.87    2.0    142ms\n",
      "  5   -2.772083417415       -8.49       -4.22    2.0    125ms\n",
      "  6   -2.772083417806       -9.41       -5.61    1.0    127ms\n",
      "  7   -2.772083417811      -11.32       -6.22    2.0    129ms\n",
      "  8   -2.772083417811      -14.07       -6.84    1.0    128ms\n",
      "  9   -2.772083417811      -14.05       -7.40    2.0    139ms\n",
      " 10   -2.772083417811   +  -14.51       -8.36    1.0    130ms\n"
     ]
    },
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "1.7735580927986587"
     },
     "metadata": {},
     "execution_count": 2
    }
   ],
   "cell_type": "code",
   "source": [
    "polarizability_fd = let\n",
    "    ε = 0.01\n",
    "    (compute_dipole(ε) - compute_dipole(0.0)) / ε\n",
    "end"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "We do the same thing using automatic differentiation. Under the hood this uses\n",
    "custom rules to implicitly differentiate through the self-consistent\n",
    "field fixed-point problem."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime\n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -2.770749865326                   -0.52    9.0         \n",
      "  2   -2.772059341424       -2.88       -1.32    1.0    103ms\n",
      "  3   -2.772083130966       -4.62       -2.48    1.0    131ms\n",
      "  4   -2.772083344826       -6.67       -3.18    1.0    107ms\n",
      "  5   -2.772083415617       -7.15       -3.85    2.0    157ms\n",
      "  6   -2.772083417666       -8.69       -4.64    1.0    110ms\n",
      "  7   -2.772083417808       -9.85       -5.22    2.0    148ms\n",
      "  8   -2.772083417810      -11.61       -6.19    1.0    114ms\n",
      "  9   -2.772083417811      -12.79       -6.63    2.0    136ms\n",
      " 10   -2.772083417811      -13.91       -7.66    1.0    122ms\n",
      " 11   -2.772083417811   +  -14.15       -8.21    1.0    126ms\n",
      "\n",
      "Polarizability via ForwardDiff:       1.7725349764199978\n",
      "Polarizability via finite difference: 1.7735580927986587\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "polarizability = ForwardDiff.derivative(compute_dipole, 0.0)\n",
    "println()\n",
    "println(\"Polarizability via ForwardDiff:       $polarizability\")\n",
    "println(\"Polarizability via finite difference: $polarizability_fd\")"
   ],
   "metadata": {},
   "execution_count": 3
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.4"
  },
  "kernelspec": {
   "name": "julia-1.9",
   "display_name": "Julia 1.9.4",
   "language": "julia"
  }
 },
 "nbformat": 4
}
