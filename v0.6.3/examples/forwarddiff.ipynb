{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Polarizability using automatic differentiation\n",
    "\n",
    "Simple example for computing properties using (forward-mode)\n",
    "automatic differentiation.\n",
    "For a more classical approach and more details about computing polarizabilities,\n",
    "see Polarizability by linear response."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using DFTK\n",
    "using LinearAlgebra\n",
    "using ForwardDiff\n",
    "\n",
    "# Construct PlaneWaveBasis given a particular electric field strength\n",
    "# Again we take the example of a Helium atom.\n",
    "function make_basis(ε::T; a=10., Ecut=30) where {T}\n",
    "    lattice=T(a) * I(3)  # lattice is a cube of $a$ Bohrs\n",
    "    # Helium at the center of the box\n",
    "    atoms     = [ElementPsp(:He, psp=load_psp(\"hgh/lda/He-q2\"))]\n",
    "    positions = [[1/2, 1/2, 1/2]]\n",
    "\n",
    "    model = model_DFT(lattice, atoms, positions, [:lda_x, :lda_c_vwn];\n",
    "                      extra_terms=[ExternalFromReal(r -> -ε * (r[1] - a/2))],\n",
    "                      symmetries=false)\n",
    "    PlaneWaveBasis(model; Ecut, kgrid=[1, 1, 1])  # No k-point sampling on isolated system\n",
    "end\n",
    "\n",
    "# dipole moment of a given density (assuming the current geometry)\n",
    "function dipole(basis, ρ)\n",
    "    @assert isdiag(basis.model.lattice)\n",
    "    a  = basis.model.lattice[1, 1]\n",
    "    rr = [a * (r[1] - 1/2) for r in r_vectors(basis)]\n",
    "    sum(rr .* ρ) * basis.dvol\n",
    "end\n",
    "\n",
    "# Function to compute the dipole for a given field strength\n",
    "function compute_dipole(ε; tol=1e-8, kwargs...)\n",
    "    scfres = self_consistent_field(make_basis(ε; kwargs...); tol)\n",
    "    dipole(scfres.basis, scfres.ρ)\n",
    "end;"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "With this in place we can compute the polarizability from finite differences\n",
    "(just like in the previous example):"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime\n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -2.770551179004                   -0.53    9.0         \n",
      "  2   -2.772137674712       -2.80       -1.29    1.0    172ms\n",
      "  3   -2.772169829649       -4.49       -2.71    1.0    240ms\n",
      "  4   -2.772170718882       -6.05       -3.73    2.0    217ms\n",
      "  5   -2.772170722755       -8.41       -4.23    1.0    186ms\n",
      "  6   -2.772170722994       -9.62       -5.46    1.0    226ms\n",
      "  7   -2.772170723013      -10.71       -5.39    2.0    221ms\n",
      "  8   -2.772170723015      -11.80       -6.24    1.0    196ms\n",
      "  9   -2.772170723015      -13.41       -6.79    1.0    249ms\n",
      " 10   -2.772170723015   +  -14.27       -7.17    2.0    244ms\n",
      " 11   -2.772170723015      -14.12       -7.93    1.0    218ms\n",
      " 12   -2.772170723015   +  -15.05       -8.11    1.0    236ms\n",
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime\n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -2.770687622457                   -0.53    9.0         \n",
      "  2   -2.772053632632       -2.86       -1.31    1.0    251ms\n",
      "  3   -2.772083111087       -4.53       -2.61    1.0    189ms\n",
      "  4   -2.772083402594       -6.54       -3.90    1.0    190ms\n",
      "  5   -2.772083417556       -7.83       -4.34    2.0    241ms\n",
      "  6   -2.772083417798       -9.62       -5.59    1.0    328ms\n",
      "  7   -2.772083417810      -10.91       -5.65    3.0    241ms\n",
      "  8   -2.772083417811      -12.18       -6.59    1.0    193ms\n",
      "  9   -2.772083417811   +  -13.78       -7.32    1.0    800ms\n",
      " 10   -2.772083417811      -13.99       -7.40    2.0    246ms\n",
      " 11   -2.772083417811      -15.35       -8.36    1.0    688ms\n"
     ]
    },
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "1.7735581202054613"
     },
     "metadata": {},
     "execution_count": 2
    }
   ],
   "cell_type": "code",
   "source": [
    "polarizability_fd = let\n",
    "    ε = 0.01\n",
    "    (compute_dipole(ε) - compute_dipole(0.0)) / ε\n",
    "end"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "We do the same thing using automatic differentiation. Under the hood this uses\n",
    "custom rules to implicitly differentiate through the self-consistent\n",
    "field fixed-point problem."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime\n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -2.770707622056                   -0.53    8.0         \n",
      "  2   -2.772055260826       -2.87       -1.31    1.0    224ms\n",
      "  3   -2.772083075044       -4.56       -2.59    1.0    177ms\n",
      "  4   -2.772083373594       -6.52       -3.51    1.0    220ms\n",
      "  5   -2.772083415644       -7.38       -3.88    2.0    208ms\n",
      "  6   -2.772083417761       -8.67       -5.28    1.0    214ms\n",
      "  7   -2.772083417805      -10.35       -5.20    3.0    244ms\n",
      "  8   -2.772083417810      -11.30       -5.74    1.0    210ms\n",
      "  9   -2.772083417811      -12.71       -6.67    1.0    209ms\n",
      " 10   -2.772083417811      -14.35       -7.35    2.0    258ms\n",
      " 11   -2.772083417811      -14.45       -7.12    2.0    251ms\n",
      " 12   -2.772083417811      -13.85       -7.94    1.0    217ms\n",
      " 13   -2.772083417811   +  -13.80       -8.97    2.0    254ms\n",
      "\n",
      "Polarizability via ForwardDiff:       1.7725349692472743\n",
      "Polarizability via finite difference: 1.7735581202054613\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "polarizability = ForwardDiff.derivative(compute_dipole, 0.0)\n",
    "println()\n",
    "println(\"Polarizability via ForwardDiff:       $polarizability\")\n",
    "println(\"Polarizability via finite difference: $polarizability_fd\")"
   ],
   "metadata": {},
   "execution_count": 3
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.5"
  },
  "kernelspec": {
   "name": "julia-1.8",
   "display_name": "Julia 1.8.5",
   "language": "julia"
  }
 },
 "nbformat": 4
}
