{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Modelling a gallium arsenide surface\n",
    "\n",
    "This example shows how to use the atomistic simulation environment or ASE for short,\n",
    "to set up and run a particular calculation of a gallium arsenide surface.\n",
    "ASE is a Python package to simplify the process of setting up,\n",
    "running and analysing results from atomistic simulations across different simulation codes.\n",
    "For more details on the integration DFTK provides with ASE,\n",
    "see Atomistic simulation environment (ASE).\n",
    "\n",
    "In this example we will consider modelling the (1, 1, 0) GaAs surface separated by vacuum."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "Parameters of the calculation. Since this surface is far from easy to converge,\n",
    "we made the problem simpler by choosing a smaller `Ecut` and smaller values\n",
    "for `n_GaAs` and `n_vacuum`.\n",
    "More interesting settings are `Ecut = 15` and `n_GaAs = n_vacuum = 20`."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "miller = (1, 1, 0)   # Surface Miller indices\n",
    "n_GaAs = 2           # Number of GaAs layers\n",
    "n_vacuum = 4         # Number of vacuum layers\n",
    "Ecut = 5             # Hartree\n",
    "kgrid = (4, 4, 1);   # Monkhorst-Pack mesh"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "Use ASE to build the structure:"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "    CondaPkg Found dependencies: /home/runner/.julia/packages/ASEconvert/pAQve/CondaPkg.toml\n",
      "    CondaPkg Found dependencies: /home/runner/.julia/packages/CondaPkg/0UqYV/CondaPkg.toml\n",
      "    CondaPkg Found dependencies: /home/runner/.julia/packages/PythonCall/mkWc2/CondaPkg.toml\n",
      "    CondaPkg Resolving changes\n",
      "             + ase\n",
      "             + libstdcxx\n",
      "             + libstdcxx-ng\n",
      "             + openssl\n",
      "             + python\n",
      "    CondaPkg Initialising pixi\n",
      "             │ /home/runner/.julia/artifacts/cefba4912c2b400756d043a2563ef77a0088866b/bin/pixi\n",
      "             │ init\n",
      "             │ --format pixi\n",
      "             └ /home/runner/work/DFTK.jl/DFTK.jl/docs/.CondaPkg\n",
      "✔ Created /home/runner/work/DFTK.jl/DFTK.jl/docs/.CondaPkg/pixi.toml\n",
      "    CondaPkg Wrote /home/runner/work/DFTK.jl/DFTK.jl/docs/.CondaPkg/pixi.toml\n",
      "             │ [dependencies]\n",
      "             │ openssl = \">=3, <3.6\"\n",
      "             │ libstdcxx = \">=3.4,<15.0\"\n",
      "             │ libstdcxx-ng = \">=3.4,<15.0\"\n",
      "             │ ase = \">=3.26,<3.27\"\n",
      "             │ \n",
      "             │     [dependencies.python]\n",
      "             │     channel = \"conda-forge\"\n",
      "             │     build = \"*cp*\"\n",
      "             │     version = \">=3.9,<4\"\n",
      "             │ \n",
      "             │ [project]\n",
      "             │ name = \".CondaPkg\"\n",
      "             │ platforms = [\"linux-64\"]\n",
      "             │ channels = [\"conda-forge\"]\n",
      "             │ channel-priority = \"strict\"\n",
      "             └ description = \"automatically generated by CondaPkg.jl\"\n",
      "    CondaPkg Installing packages\n",
      "             │ /home/runner/.julia/artifacts/cefba4912c2b400756d043a2563ef77a0088866b/bin/pixi\n",
      "             │ install\n",
      "             └ --manifest-path /home/runner/work/DFTK.jl/DFTK.jl/docs/.CondaPkg/pixi.toml\n",
      "✔ The default environment has been installed.\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "using ASEconvert\n",
    "using PythonCall\n",
    "\n",
    "a = 5.6537  # GaAs lattice parameter in Ångström (because ASE uses Å as length unit)\n",
    "gaas = ase.build.bulk(\"GaAs\", \"zincblende\"; a)\n",
    "surface = ase.build.surface(gaas, miller, n_GaAs, 0, periodic=true);"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "Get the amount of vacuum in Ångström we need to add"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "d_vacuum = maximum(maximum, surface.cell) / n_GaAs * n_vacuum\n",
    "surface = ase.build.surface(gaas, miller, n_GaAs, d_vacuum, periodic=true);"
   ],
   "metadata": {},
   "execution_count": 3
  },
  {
   "cell_type": "markdown",
   "source": [
    "Write an image of the surface and embed it as a nice illustration:"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "Python: None"
     },
     "metadata": {},
     "execution_count": 4
    }
   ],
   "cell_type": "code",
   "source": [
    "ase.io.write(\"surface.png\", surface * pytuple((3, 3, 1)), rotation=\"-90x, 30y, -75z\")"
   ],
   "metadata": {},
   "execution_count": 4
  },
  {
   "cell_type": "markdown",
   "source": [
    "<img src=\"https://docs.dftk.org/stable/surface.png\" width=500 height=500 />"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "Use the `pyconvert` function from `PythonCall` to convert the ASE atoms\n",
    "to an AtomsBase-compatible system.\n",
    "This can then be used in the same way as other `AtomsBase` systems\n",
    "(see AtomsBase integration for details) to construct a DFTK model:"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "Model(gga_x_pbe+gga_c_pbe, 3D):\n    lattice (in Bohr)    : [7.55469   , 0         , 0         ]\n                           [0         , 7.55469   , 0         ]\n                           [0         , 0         , 40.0648   ]\n    unit cell volume     : 2286.6 Bohr³\n\n    atoms                : As₂Ga₂\n    pseudopot. family    : PseudoFamily(\"cp2k.nc.sr.pbe.v0_1.largecore.gth\")\n\n    num. electrons       : 16\n    spin polarization    : none\n    temperature          : 0.001 Ha\n    smearing             : DFTK.Smearing.Gaussian()\n\n    terms                : Kinetic()\n                           AtomicLocal()\n                           AtomicNonlocal()\n                           Ewald(nothing)\n                           PspCorrection()\n                           Hartree()\n                           Xc(gga_x_pbe, gga_c_pbe)\n                           Entropy()"
     },
     "metadata": {},
     "execution_count": 5
    }
   ],
   "cell_type": "code",
   "source": [
    "using DFTK\n",
    "using PseudoPotentialData\n",
    "\n",
    "pseudopotentials = PseudoFamily(\"cp2k.nc.sr.pbe.v0_1.largecore.gth\")\n",
    "model = model_DFT(pyconvert(AbstractSystem, surface);\n",
    "                  functionals=PBE(),\n",
    "                  temperature=1e-3,\n",
    "                  smearing=DFTK.Smearing.Gaussian(),\n",
    "                  pseudopotentials)"
   ],
   "metadata": {},
   "execution_count": 5
  },
  {
   "cell_type": "markdown",
   "source": [
    "In the above we use the `pseudopotential` keyword argument to\n",
    "assign the respective pseudopotentials to the imported `model.atoms`.\n",
    "Try lowering the SCF convergence tolerance (`tol`)\n",
    "or try `mixing=KerkerMixing()` to see the full challenge of this system."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime\n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -16.58666216102                   -0.58    5.3    1.72s\n",
      "  2   -16.72510988817       -0.86       -1.01    1.0    305ms\n",
      "  3   -16.73051443546       -2.27       -1.57    2.3    299ms\n",
      "  4   -16.73121822817       -3.15       -2.16    1.0    245ms\n",
      "  5   -16.73132408196       -3.98       -2.58    2.0    285ms\n",
      "  6   -16.73133243394       -5.08       -2.83    2.0    287ms\n",
      "  7   -16.73063436712   +   -3.16       -2.39    2.3    322ms\n",
      "  8   -16.73132862806       -3.16       -3.00    2.1    276ms\n",
      "  9   -16.73131164593   +   -4.77       -3.06    2.0    602ms\n",
      " 10   -16.73131419400       -5.59       -3.08    1.8    244ms\n",
      " 11   -16.73133614032       -4.66       -3.46    1.1    228ms\n",
      " 12   -16.73133796426       -5.74       -3.58    1.0    222ms\n",
      " 13   -16.73133959168       -5.79       -3.86    1.0    219ms\n",
      " 14   -16.73134019584       -6.22       -4.73    1.4    234ms\n",
      " 15   -16.73134018522   +   -7.97       -4.64    2.3    321ms\n",
      " 16   -16.73134018634       -8.95       -4.66    1.0    226ms\n",
      " 17   -16.73134018888       -8.60       -4.69    1.0    223ms\n",
      " 18   -16.73134020026       -7.94       -5.33    1.0    225ms\n",
      " 19   -16.73134019985   +   -9.39       -5.23    2.2    283ms\n",
      " 20   -16.73134019999       -9.86       -5.41    1.1    229ms\n",
      " 21   -16.73134020043       -9.36       -6.20    1.2    232ms\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "basis  = PlaneWaveBasis(model; Ecut, kgrid)\n",
    "scfres = self_consistent_field(basis; tol=1e-6, mixing=LdosMixing());"
   ],
   "metadata": {},
   "execution_count": 6
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "Energy breakdown (in Ha):\n    Kinetic             5.8593969 \n    AtomicLocal         -105.6100097\n    AtomicNonlocal      2.3494809 \n    Ewald               35.5044300\n    PspCorrection       0.2016043 \n    Hartree             49.5614245\n    Xc                  -4.5976636\n    Entropy             -0.0000035\n\n    total               -16.731340200430"
     },
     "metadata": {},
     "execution_count": 7
    }
   ],
   "cell_type": "code",
   "source": [
    "scfres.energies"
   ],
   "metadata": {},
   "execution_count": 7
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.7"
  },
  "kernelspec": {
   "name": "julia-1.11",
   "display_name": "Julia 1.11.7",
   "language": "julia"
  }
 },
 "nbformat": 4
}
