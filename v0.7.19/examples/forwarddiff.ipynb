{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Polarizability using automatic differentiation\n",
    "\n",
    "Simple example for computing properties using (forward-mode)\n",
    "automatic differentiation.\n",
    "For a more classical approach and more details about computing polarizabilities,\n",
    "see Polarizability by linear response."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using DFTK\n",
    "using LinearAlgebra\n",
    "using ForwardDiff\n",
    "using PseudoPotentialData\n",
    "\n",
    "# Construct PlaneWaveBasis given a particular electric field strength\n",
    "# Again we take the example of a Helium atom.\n",
    "function make_basis(ε::T; a=10., Ecut=30) where {T}\n",
    "    lattice = T(a) * I(3)  # lattice is a cube of $a$ Bohrs\n",
    "    # Helium at the center of the box\n",
    "    pseudopotentials = PseudoFamily(\"cp2k.nc.sr.lda.v0_1.semicore.gth\")\n",
    "    atoms     = [ElementPsp(:He, pseudopotentials)]\n",
    "    positions = [[1/2, 1/2, 1/2]]\n",
    "\n",
    "    model = model_DFT(lattice, atoms, positions;\n",
    "                      functionals=[:lda_x, :lda_c_vwn],\n",
    "                      extra_terms=[ExternalFromReal(r -> -ε * (r[1] - a/2))],\n",
    "                      symmetries=false)\n",
    "    PlaneWaveBasis(model; Ecut, kgrid=[1, 1, 1])  # No k-point sampling on isolated system\n",
    "end\n",
    "\n",
    "# dipole moment of a given density (assuming the current geometry)\n",
    "function dipole(basis, ρ)\n",
    "    @assert isdiag(basis.model.lattice)\n",
    "    a  = basis.model.lattice[1, 1]\n",
    "    rr = [a * (r[1] - 1/2) for r in r_vectors(basis)]\n",
    "    sum(rr .* ρ) * basis.dvol\n",
    "end\n",
    "\n",
    "# Function to compute the dipole for a given field strength\n",
    "function compute_dipole(ε; tol=1e-8, kwargs...)\n",
    "    scfres = self_consistent_field(make_basis(ε; kwargs...); tol)\n",
    "    dipole(scfres.basis, scfres.ρ)\n",
    "end;"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "With this in place we can compute the polarizability from finite differences\n",
    "(just like in the previous example):"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime\n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -2.770891623570                   -0.52    9.0    198ms\n",
      "  2   -2.772148294052       -2.90       -1.32    1.0    100ms\n",
      "  3   -2.772170251682       -4.66       -2.45    1.0    119ms\n",
      "  4   -2.772170649368       -6.40       -3.16    1.0    104ms\n",
      "  5   -2.772170722623       -7.14       -4.08    2.0    124ms\n",
      "  6   -2.772170722920       -9.53       -4.47    1.0    113ms\n",
      "  7   -2.772170723007      -10.06       -5.18    1.0    112ms\n",
      "  8   -2.772170723014      -11.18       -5.73    1.0    118ms\n",
      "  9   -2.772170723015      -12.00       -6.33    2.0    132ms\n",
      " 10   -2.772170723015      -13.60       -6.95    1.0    117ms\n",
      " 11   -2.772170723015   +  -14.15       -7.59    2.0    133ms\n",
      " 12   -2.772170723015      -14.24       -8.17    1.0    116ms\n",
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime\n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -2.770768916441                   -0.52    9.0    173ms\n",
      "  2   -2.772060073108       -2.89       -1.33    1.0    116ms\n",
      "  3   -2.772082903309       -4.64       -2.41    1.0    104ms\n",
      "  4   -2.772083336521       -6.36       -3.12    1.0    231ms\n",
      "  5   -2.772083417747       -7.09       -4.77    2.0    919ms\n",
      "  6   -2.772083417788      -10.38       -4.83    2.0    135ms\n",
      "  7   -2.772083417809      -10.68       -5.61    1.0    121ms\n",
      "  8   -2.772083417811      -11.95       -6.58    2.0    143ms\n",
      "  9   -2.772083417811      -13.67       -7.74    1.0    151ms\n",
      " 10   -2.772083417811   +  -14.03       -8.31    1.0    146ms\n"
     ]
    },
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "1.7735579817892122"
     },
     "metadata": {},
     "execution_count": 2
    }
   ],
   "cell_type": "code",
   "source": [
    "polarizability_fd = let\n",
    "    ε = 0.01\n",
    "    (compute_dipole(ε) - compute_dipole(0.0)) / ε\n",
    "end"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "We do the same thing using automatic differentiation. Under the hood this uses\n",
    "custom rules to implicitly differentiate through the self-consistent\n",
    "field fixed-point problem. This leads to a density-functional perturbation\n",
    "theory problem, which is automatically set up and solved in the background."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime\n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -2.770777714548                   -0.53    9.0    230ms\n",
      "  2   -2.772050788734       -2.90       -1.31    1.0    127ms\n",
      "  3   -2.772082852719       -4.49       -2.62    1.0    103ms\n",
      "  4   -2.772083392947       -6.27       -4.13    1.0    107ms\n",
      "  5   -2.772083417776       -7.61       -4.99    2.0    124ms\n",
      "  6   -2.772083417809      -10.48       -5.69    2.0    128ms\n",
      "  7   -2.772083417811      -11.88       -6.23    1.0    113ms\n",
      "  8   -2.772083417811      -13.29       -6.87    2.0    131ms\n",
      "  9   -2.772083417811   +  -14.21       -8.16    1.0    112ms\n",
      "Solving response problem\n",
      "Iter  Restart  Krydim  log10(res)  avg(CG)  Δtime   Comment\n",
      "----  -------  ------  ----------  -------  ------  ---------------\n",
      "                                      13.0  98.4ms  Non-interacting\n",
      "   1        0       1       -0.60     10.0   147ms  \n",
      "   2        0       2       -2.42      8.0   130ms  \n",
      "   3        0       3       -3.55      6.0   111ms  \n",
      "   4        0       4       -5.33      5.0   106ms  \n",
      "   5        0       5       -7.17      1.0  87.9ms  \n",
      "   6        0       6      -10.29      1.0  91.4ms  \n",
      "   7        1       1       -7.56     13.0   225ms  Restart\n",
      "   8        1       2       -9.07      1.0  87.1ms  \n",
      "                                      13.0   125ms  Final orbitals\n",
      "\n",
      "Polarizability via ForwardDiff:       1.7725349654291152\n",
      "Polarizability via finite difference: 1.7735579817892122\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "polarizability = ForwardDiff.derivative(compute_dipole, 0.0)\n",
    "println()\n",
    "println(\"Polarizability via ForwardDiff:       $polarizability\")\n",
    "println(\"Polarizability via finite difference: $polarizability_fd\")"
   ],
   "metadata": {},
   "execution_count": 3
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.1"
  },
  "kernelspec": {
   "name": "julia-1.12",
   "display_name": "Julia 1.12.1",
   "language": "julia"
  }
 },
 "nbformat": 4
}
