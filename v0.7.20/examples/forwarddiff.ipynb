{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Polarizability using automatic differentiation\n",
    "\n",
    "Simple example for computing properties using (forward-mode)\n",
    "automatic differentiation.\n",
    "For a more classical approach and more details about computing polarizabilities,\n",
    "see Polarizability by linear response."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using DFTK\n",
    "using LinearAlgebra\n",
    "using ForwardDiff\n",
    "using PseudoPotentialData\n",
    "\n",
    "# Construct PlaneWaveBasis given a particular electric field strength\n",
    "# Again we take the example of a Helium atom.\n",
    "function make_basis(ε::T; a=10., Ecut=30) where {T}\n",
    "    lattice = T(a) * I(3)  # lattice is a cube of $a$ Bohrs\n",
    "    # Helium at the center of the box\n",
    "    pseudopotentials = PseudoFamily(\"cp2k.nc.sr.lda.v0_1.semicore.gth\")\n",
    "    atoms     = [ElementPsp(:He, pseudopotentials)]\n",
    "    positions = [[1/2, 1/2, 1/2]]\n",
    "\n",
    "    model = model_DFT(lattice, atoms, positions;\n",
    "                      functionals=[:lda_x, :lda_c_vwn],\n",
    "                      extra_terms=[ExternalFromReal(r -> -ε * (r[1] - a/2))],\n",
    "                      symmetries=false)\n",
    "    PlaneWaveBasis(model; Ecut, kgrid=[1, 1, 1])  # No k-point sampling on isolated system\n",
    "end\n",
    "\n",
    "# dipole moment of a given density (assuming the current geometry)\n",
    "function dipole(basis, ρ)\n",
    "    @assert isdiag(basis.model.lattice)\n",
    "    a  = basis.model.lattice[1, 1]\n",
    "    rr = [a * (r[1] - 1/2) for r in r_vectors(basis)]\n",
    "    sum(rr .* ρ) * basis.dvol\n",
    "end\n",
    "\n",
    "# Function to compute the dipole for a given field strength\n",
    "function compute_dipole(ε; tol=1e-8, kwargs...)\n",
    "    scfres = self_consistent_field(make_basis(ε; kwargs...); tol)\n",
    "    dipole(scfres.basis, scfres.ρ)\n",
    "end;"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "With this in place we can compute the polarizability from finite differences\n",
    "(just like in the previous example):"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime \n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -2.770788772197                   -0.53    8.0    155ms\n",
      "  2   -2.772137236100       -2.87       -1.31    1.0    120ms\n",
      "  3   -2.772169852427       -4.49       -2.61    1.0   99.2ms\n",
      "  4   -2.772170720262       -6.06       -4.06    2.0    131ms\n",
      "  5   -2.772170722909       -8.58       -4.52    2.0    122ms\n",
      "  6   -2.772170723012       -9.99       -5.82    1.0    107ms\n",
      "  7   -2.772170723015      -11.50       -6.40    2.0    128ms\n",
      "  8   -2.772170723015      -13.83       -6.74    2.0    123ms\n",
      "  9   -2.772170723015      -13.94       -7.98    1.0    940ms\n",
      " 10   -2.772170723015      -14.45       -8.92    2.0    138ms\n",
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime \n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -2.770827882145                   -0.53    9.0    202ms\n",
      "  2   -2.772058849015       -2.91       -1.31    1.0    134ms\n",
      "  3   -2.772083192150       -4.61       -2.55    1.0    137ms\n",
      "  4   -2.772083385220       -6.71       -3.43    1.0    138ms\n",
      "  5   -2.772083416384       -7.51       -3.96    2.0    141ms\n",
      "  6   -2.772083417778       -8.86       -4.89    1.0    106ms\n",
      "  7   -2.772083417809      -10.51       -5.52    1.0    108ms\n",
      "  8   -2.772083417811      -11.73       -6.24    2.0    139ms\n",
      "  9   -2.772083417811      -13.75       -6.94    1.0    110ms\n",
      " 10   -2.772083417811      -14.45       -7.44    1.0    116ms\n",
      " 11   -2.772083417811      -15.35       -8.16    1.0    111ms\n"
     ]
    },
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "1.773558015992634"
     },
     "metadata": {},
     "execution_count": 2
    }
   ],
   "cell_type": "code",
   "source": [
    "polarizability_fd = let\n",
    "    ε = 0.01\n",
    "    (compute_dipole(ε) - compute_dipole(0.0)) / ε\n",
    "end"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "We do the same thing using automatic differentiation. Under the hood this uses\n",
    "custom rules to implicitly differentiate through the self-consistent\n",
    "field fixed-point problem. This leads to a density-functional perturbation\n",
    "theory problem, which is automatically set up and solved in the background."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime \n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -2.770775176658                   -0.52    9.0    202ms\n",
      "  2   -2.772057204464       -2.89       -1.32    1.0   98.9ms\n",
      "  3   -2.772083139816       -4.59       -2.51    1.0    116ms\n",
      "  4   -2.772083364859       -6.65       -3.30    1.0    106ms\n",
      "  5   -2.772083414566       -7.30       -3.78    2.0    125ms\n",
      "  6   -2.772083417742       -8.50       -5.25    1.0    109ms\n",
      "  7   -2.772083417810      -10.17       -5.46    2.0    129ms\n",
      "  8   -2.772083417811      -12.15       -6.12    1.0    118ms\n",
      "  9   -2.772083417811      -13.04       -6.73    2.0    129ms\n",
      " 10   -2.772083417811      -14.31       -7.29    1.0    116ms\n",
      " 11   -2.772083417811      -14.03       -8.00    1.0    116ms\n",
      "Solving response problem\n",
      "Iter  Restart  Krydim  log10(res)  avg(CG)  Δtime   Comment\n",
      "----  -------  ------  ----------  -------  ------  ---------------\n",
      "                                      13.0  73.9ms  Non-interacting\n",
      "   1        0       1       -0.60     10.0   251ms  \n",
      "   2        0       2       -2.42      8.0   798ms  \n",
      "   3        0       3       -3.55      5.0   101ms  \n",
      "   4        0       4       -5.33      4.0   101ms  \n",
      "   5        0       5       -7.21      1.0  83.1ms  \n",
      "   6        0       6      -10.39      1.0  88.7ms  \n",
      "   7        1       1       -7.33     13.0   216ms  Restart\n",
      "   8        1       2       -8.92      1.0  85.2ms  \n",
      "                                      13.0   121ms  Final orbitals\n",
      "\n",
      "Polarizability via ForwardDiff:       1.7725349764349285\n",
      "Polarizability via finite difference: 1.773558015992634\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "polarizability = ForwardDiff.derivative(compute_dipole, 0.0)\n",
    "println()\n",
    "println(\"Polarizability via ForwardDiff:       $polarizability\")\n",
    "println(\"Polarizability via finite difference: $polarizability_fd\")"
   ],
   "metadata": {},
   "execution_count": 3
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.3"
  },
  "kernelspec": {
   "name": "julia-1.12",
   "display_name": "Julia 1.12.3",
   "language": "julia"
  }
 },
 "nbformat": 4
}
