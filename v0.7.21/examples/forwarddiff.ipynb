{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Polarizability using automatic differentiation\n",
    "\n",
    "Simple example for computing properties using (forward-mode)\n",
    "automatic differentiation.\n",
    "For a more classical approach and more details about computing polarizabilities,\n",
    "see Polarizability by linear response."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using DFTK\n",
    "using LinearAlgebra\n",
    "using ForwardDiff\n",
    "using PseudoPotentialData\n",
    "\n",
    "# Construct PlaneWaveBasis given a particular electric field strength\n",
    "# Again we take the example of a Helium atom.\n",
    "function make_basis(ε::T; a=10., Ecut=30) where {T}\n",
    "    lattice = T(a) * I(3)  # lattice is a cube of $a$ Bohrs\n",
    "    # Helium at the center of the box\n",
    "    pseudopotentials = PseudoFamily(\"cp2k.nc.sr.lda.v0_1.semicore.gth\")\n",
    "    atoms     = [ElementPsp(:He, pseudopotentials)]\n",
    "    positions = [[1/2, 1/2, 1/2]]\n",
    "\n",
    "    model = model_DFT(lattice, atoms, positions;\n",
    "                      functionals=[:lda_x, :lda_c_vwn],\n",
    "                      extra_terms=[ExternalFromReal(r -> -ε * (r[1] - a/2))],\n",
    "                      symmetries=false)\n",
    "    PlaneWaveBasis(model; Ecut, kgrid=[1, 1, 1])  # No k-point sampling on isolated system\n",
    "end\n",
    "\n",
    "# dipole moment of a given density (assuming the current geometry)\n",
    "function dipole(basis, ρ)\n",
    "    @assert isdiag(basis.model.lattice)\n",
    "    a  = basis.model.lattice[1, 1]\n",
    "    rr = [a * (r[1] - 1/2) for r in r_vectors(basis)]\n",
    "    sum(rr .* ρ) * basis.dvol\n",
    "end\n",
    "\n",
    "# Function to compute the dipole for a given field strength\n",
    "function compute_dipole(ε; tol=1e-8, kwargs...)\n",
    "    scfres = self_consistent_field(make_basis(ε; kwargs...); tol)\n",
    "    dipole(scfres.basis, scfres.ρ)\n",
    "end;"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "With this in place we can compute the polarizability from finite differences\n",
    "(just like in the previous example):"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime \n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -2.770857510647                   -0.52    9.0    174ms\n",
      "  2   -2.772147933039       -2.89       -1.32    1.0    122ms\n",
      "  3   -2.772170254419       -4.65       -2.44    1.0    102ms\n",
      "  4   -2.772170647895       -6.41       -3.15    1.0    106ms\n",
      "  5   -2.772170722761       -7.13       -4.25    2.0    124ms\n",
      "  6   -2.772170722853      -10.04       -4.38    1.0    109ms\n",
      "  7   -2.772170723012       -9.80       -5.29    1.0    116ms\n",
      "  8   -2.772170723014      -11.71       -5.38    1.0    117ms\n",
      "  9   -2.772170723015      -11.91       -6.00    1.0    118ms\n",
      " 10   -2.772170723015      -12.78       -6.66    2.0    206ms\n",
      " 11   -2.772170723015   +  -14.24       -6.99    1.0    862ms\n",
      " 12   -2.772170723015   +  -15.35       -7.73    1.0    113ms\n",
      " 13   -2.772170723015      -14.15       -8.07    1.0    113ms\n",
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime \n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -2.770827781778                   -0.52    9.0    201ms\n",
      "  2   -2.772062849294       -2.91       -1.32    1.0    132ms\n",
      "  3   -2.772083058267       -4.69       -2.45    1.0    124ms\n",
      "  4   -2.772083348187       -6.54       -3.16    1.0    104ms\n",
      "  5   -2.772083417569       -7.16       -4.26    2.0    125ms\n",
      "  6   -2.772083417733       -9.79       -4.65    1.0    121ms\n",
      "  7   -2.772083417809      -10.12       -5.97    1.0    109ms\n",
      "  8   -2.772083417811      -11.83       -6.78    2.0    125ms\n",
      "  9   -2.772083417811   +  -14.05       -7.29    1.0    115ms\n",
      " 10   -2.772083417811      -13.91       -8.13    2.0    126ms\n"
     ]
    },
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "1.7735579634498053"
     },
     "metadata": {},
     "execution_count": 2
    }
   ],
   "cell_type": "code",
   "source": [
    "polarizability_fd = let\n",
    "    ε = 0.01\n",
    "    (compute_dipole(ε) - compute_dipole(0.0)) / ε\n",
    "end"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "We do the same thing using automatic differentiation. Under the hood this uses\n",
    "custom rules to implicitly differentiate through the self-consistent\n",
    "field fixed-point problem. This leads to a density-functional perturbation\n",
    "theory problem, which is automatically set up and solved in the background."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime \n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -2.770732543230                   -0.53   10.0    184ms\n",
      "  2   -2.772049378019       -2.88       -1.31    1.0    132ms\n",
      "  3   -2.772082358629       -4.48       -2.64    1.0    119ms\n",
      "  4   -2.772083413786       -5.98       -3.79    2.0    123ms\n",
      "  5   -2.772083417509       -8.43       -4.29    1.0    109ms\n",
      "  6   -2.772083417785       -9.56       -5.15    1.0    113ms\n",
      "  7   -2.772083417810      -10.62       -5.52    2.0    132ms\n",
      "  8   -2.772083417811      -11.98       -6.67    1.0    115ms\n",
      "  9   -2.772083417811      -13.75       -6.77    2.0    133ms\n",
      " 10   -2.772083417811      -14.15       -7.07    1.0    115ms\n",
      " 11   -2.772083417811   +  -14.18       -8.18    1.0    121ms\n",
      "Solving response problem\n",
      "Iter  Restart  Krydim  log10(res)  avg(CG)  Δtime   Comment\n",
      "----  -------  ------  ----------  -------  ------  ---------------\n",
      "                                      13.0  98.0ms  Non-interacting\n",
      "   1        0       1       -0.60     10.0   147ms  \n",
      "   2        0       2       -2.42      8.0   129ms  \n",
      "   3        0       3       -3.55      6.0   109ms  \n",
      "   4        0       4       -5.33      5.0   102ms  \n",
      "   5        0       5       -7.17      1.0   921ms  \n",
      "   6        0       6      -10.27      1.0  83.9ms  \n",
      "   7        1       1       -7.56     13.0   214ms  Restart\n",
      "   8        1       2       -9.07      1.0  83.2ms  \n",
      "                                      13.0   120ms  Final orbitals\n",
      "\n",
      "Polarizability via ForwardDiff:       1.7725349635022591\n",
      "Polarizability via finite difference: 1.7735579634498053\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "polarizability = ForwardDiff.derivative(compute_dipole, 0.0)\n",
    "println()\n",
    "println(\"Polarizability via ForwardDiff:       $polarizability\")\n",
    "println(\"Polarizability via finite difference: $polarizability_fd\")"
   ],
   "metadata": {},
   "execution_count": 3
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.4"
  },
  "kernelspec": {
   "name": "julia-1.12",
   "display_name": "Julia 1.12.4",
   "language": "julia"
  }
 },
 "nbformat": 4
}
