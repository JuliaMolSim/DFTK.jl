{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Creating and modelling metallic supercells\n",
    "\n",
    "In this section we will be concerned with modelling supercells of aluminium.\n",
    "When dealing with periodic problems there is no unique definition of the\n",
    "lattice: Clearly any duplication of the lattice along an axis is also a valid\n",
    "repetitive unit to describe exactly the same system.\n",
    "This is exactly what a **supercell** is: An $n$-fold repetition along one (or multiple)\n",
    "axes of the original lattice.\n",
    "\n",
    "The following code achieves this for aluminium:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using AtomsBuilder\n",
    "using DFTK\n",
    "using LinearAlgebra\n",
    "using Unitful\n",
    "using UnitfulAtomic\n",
    "using PseudoPotentialData\n",
    "\n",
    "function aluminium_setup(repeat=1; Ecut=7.0, kgrid=[2, 2, 2])\n",
    "    # Use AtomsBuilder to setup aluminium cubic unit cell (4 Al atoms)\n",
    "    # with provided lattice constant, see AtomsBase integration for details.\n",
    "    unit_cell = bulk(:Al; a=7.65339u\"bohr\", cubic=true)\n",
    "    supercell = unit_cell * (repeat, 1, 1)  # Make a supercell\n",
    "\n",
    "    # Select standard pseudodojo pseudopotentials, construct an LDA model, discretize\n",
    "    # Note: We disable symmetries explicitly here. Otherwise the problem sizes\n",
    "    #       we are able to run on the CI are too simple to observe the numerical\n",
    "    #       instabilities we want to trigger here.\n",
    "    pseudopotentials = PseudoFamily(\"dojo.nc.sr.lda.v0_4_1.standard.upf\")\n",
    "    model = model_DFT(supercell; pseudopotentials, functionals=LDA(),\n",
    "                      temperature=1e-3, symmetries=false)\n",
    "    PlaneWaveBasis(model; Ecut, kgrid)\n",
    "end;"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "As expected we obtain the unit cell for `repeat=1`:"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "PlaneWaveBasis discretization:\n    architecture         : DFTK.CPU()\n    num. mpi processes   : 1\n    num. julia threads   : 1\n    num. DFTK  threads   : 1\n    num. blas  threads   : 2\n    num. fft   threads   : 1\n\n    Ecut                 : 7.0 Ha\n    fft_size             : (24, 24, 24), 13824 total points\n    kgrid                : MonkhorstPack([2, 2, 2])\n    num.   red. kpoints  : 8\n    num. irred. kpoints  : 8\n\n    Estimated memory usage (per MPI process):\n        nonlocal projectors  :   3.42MiB\n        single ψ             :    486KiB\n        single ρ             :    108KiB\n        peak memory (SCF)    :   5.99MiB\n    \n    Discretized Model(lda_x+lda_c_pw, 3D):\n        lattice (in Bohr)    : [7.65339   , 0         , 0         ]\n                               [0         , 7.65339   , 0         ]\n                               [0         , 0         , 7.65339   ]\n        unit cell volume     : 448.29 Bohr³\n    \n        atoms                : Al₄\n        pseudopot. family    : PseudoFamily(\"dojo.nc.sr.lda.v0_4_1.standard.upf\")\n    \n        num. electrons       : 12\n        spin polarization    : none\n        temperature          : 0.001 Ha\n        smearing             : DFTK.Smearing.FermiDirac()\n    \n        terms                : Kinetic()\n                               AtomicLocal()\n                               AtomicNonlocal()\n                               Ewald(nothing)\n                               PspCorrection()\n                               Hartree()\n                               Xc(lda_x, lda_c_pw)\n                               Entropy()"
     },
     "metadata": {},
     "execution_count": 2
    }
   ],
   "cell_type": "code",
   "source": [
    "aluminium_setup(1)"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "and 5-fold as large supercell with `repeat=5`:"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "PlaneWaveBasis discretization:\n    architecture         : DFTK.CPU()\n    num. mpi processes   : 1\n    num. julia threads   : 1\n    num. DFTK  threads   : 1\n    num. blas  threads   : 2\n    num. fft   threads   : 1\n\n    Ecut                 : 7.0 Ha\n    fft_size             : (96, 24, 24), 55296 total points\n    kgrid                : MonkhorstPack([2, 2, 2])\n    num.   red. kpoints  : 8\n    num. irred. kpoints  : 8\n\n    Estimated memory usage (per MPI process):\n        nonlocal projectors  :   88.3MiB\n        single ψ             :   8.83MiB\n        single ρ             :    432KiB\n        peak memory (SCF)    :    118MiB\n    \n    Discretized Model(lda_x+lda_c_pw, 3D):\n        lattice (in Bohr)    : [38.267    , 0         , 0         ]\n                               [0         , 7.65339   , 0         ]\n                               [0         , 0         , 7.65339   ]\n        unit cell volume     : 2241.5 Bohr³\n    \n        atoms                : Al₂₀\n        pseudopot. family    : PseudoFamily(\"dojo.nc.sr.lda.v0_4_1.standard.upf\")\n    \n        num. electrons       : 60\n        spin polarization    : none\n        temperature          : 0.001 Ha\n        smearing             : DFTK.Smearing.FermiDirac()\n    \n        terms                : Kinetic()\n                               AtomicLocal()\n                               AtomicNonlocal()\n                               Ewald(nothing)\n                               PspCorrection()\n                               Hartree()\n                               Xc(lda_x, lda_c_pw)\n                               Entropy()"
     },
     "metadata": {},
     "execution_count": 3
    }
   ],
   "cell_type": "code",
   "source": [
    "aluminium_setup(5)"
   ],
   "metadata": {},
   "execution_count": 3
  },
  {
   "cell_type": "markdown",
   "source": [
    "As we will see in this notebook the modelling of a system generally becomes\n",
    "harder if the system becomes larger.\n",
    "\n",
    "- This sounds like a trivial statement as *per se* the cost per SCF step increases\n",
    "  as the system (and thus $N$) gets larger.\n",
    "- But there is more to it:\n",
    "  If one is not careful also the *number of SCF iterations* increases\n",
    "  as the system gets larger.\n",
    "- The aim of a proper computational treatment of such supercells is therefore\n",
    "  to ensure that the **number of SCF iterations remains constant** when the\n",
    "  system size increases."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "For achieving the latter DFTK by default employs the `LdosMixing`\n",
    "preconditioner [^HL2021] during the SCF iterations. This mixing approach is\n",
    "completely parameter free, but still automatically adapts to the treated\n",
    "system in order to efficiently prevent charge sloshing. As a result,\n",
    "modelling aluminium slabs indeed takes roughly the same number of SCF iterations\n",
    "irrespective of the supercell size:\n",
    "\n",
    "[^HL2021]:\n",
    "   M. F. Herbst and A. Levitt.\n",
    "   *Black-box inhomogeneous preconditioning for self-consistent field iterations in density functional theory.*\n",
    "   J. Phys. Cond. Matt *33* 085503 (2021). [ArXiv:2009.01665](https://arxiv.org/abs/2009.01665)"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime \n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -9.355358374704                   -1.10    6.4    178ms\n",
      "  2   -9.356810763815       -2.84       -1.43    1.0   88.1ms\n",
      "  3   -9.357074741554       -3.58       -2.78    2.4    104ms\n",
      "  4   -9.357110940185       -4.44       -3.02    8.0    225ms\n",
      "  5   -9.357111227969       -6.54       -3.14    1.0   85.6ms\n",
      "  6   -9.357111372492       -6.84       -3.29    1.0   84.1ms\n",
      "  7   -9.357111455793       -7.08       -3.45    1.0   84.4ms\n",
      "  8   -9.357111488210       -7.49       -3.61    1.0   84.9ms\n",
      "  9   -9.357111504378       -7.79       -3.90    1.1   86.1ms\n",
      " 10   -9.357111507697       -8.48       -4.15    1.2   87.6ms\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "self_consistent_field(aluminium_setup(1); tol=1e-4);"
   ],
   "metadata": {},
   "execution_count": 4
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime \n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -18.74740684555                   -0.97    6.6    1.04s\n",
      "┌ Warning: Eigensolver not converged\n",
      "│   n_iter =\n",
      "│    8-element Vector{Int64}:\n",
      "│     3\n",
      "│     1\n",
      "│     1\n",
      "│     7\n",
      "│     1\n",
      "│     7\n",
      "│     1\n",
      "│     1\n",
      "└ @ DFTK ~/work/DFTK.jl/DFTK.jl/src/scf/self_consistent_field.jl:80\n",
      "  2   -18.75907405262       -1.93       -1.32    2.8    364ms\n",
      "  3   -18.79158879671       -1.49       -2.17    4.5    372ms\n",
      "  4   -18.79259745186       -3.00       -2.50    5.0    445ms\n",
      "  5   -18.79260436880       -5.16       -3.29    2.6    317ms\n",
      "  6   -18.79260623337       -5.73       -3.58    5.2    454ms\n",
      "  7   -18.79260638341       -6.82       -3.95    1.6    227ms\n",
      "  8   -18.79260639283       -8.03       -4.44    2.9    252ms\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "self_consistent_field(aluminium_setup(2); tol=1e-4);"
   ],
   "metadata": {},
   "execution_count": 5
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime \n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -37.55027280151                   -0.84    9.1    1.74s\n",
      "  2   -37.55781317873       -2.12       -1.22    2.0    776ms\n",
      "┌ Warning: Eigensolver not converged\n",
      "│   n_iter =\n",
      "│    8-element Vector{Int64}:\n",
      "│     13\n",
      "│      8\n",
      "│     14\n",
      "│     16\n",
      "│     18\n",
      "│     18\n",
      "│     11\n",
      "│     14\n",
      "└ @ DFTK ~/work/DFTK.jl/DFTK.jl/src/scf/self_consistent_field.jl:80\n",
      "  3   -37.56450653673       -2.17       -2.11   14.0    2.11s\n",
      "  4   -37.56481245649       -3.51       -2.32   10.2    1.50s\n",
      "  5   -37.56494262488       -3.89       -3.09    1.5    809ms\n",
      "  6   -37.56494677326       -5.38       -3.48    6.9    1.40s\n",
      "  7   -37.56494967837       -5.54       -3.93    3.0    903ms\n",
      "  8   -37.56494986363       -6.73       -4.46    4.1    1.15s\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "self_consistent_field(aluminium_setup(4); tol=1e-4);"
   ],
   "metadata": {},
   "execution_count": 6
  },
  {
   "cell_type": "markdown",
   "source": [
    "When switching off explicitly the `LdosMixing`, by selecting `mixing=SimpleMixing()`,\n",
    "the performance of number of required SCF steps starts to increase as we increase\n",
    "the size of the modelled problem:"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime \n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -9.355428227663                   -1.10    5.9    708ms\n",
      "  2   -9.356856836375       -2.85       -1.90    1.0    229ms\n",
      "  3   -9.357054257302       -3.70       -2.41    5.9    152ms\n",
      "  4   -9.357098082065       -4.36       -2.68    2.9    104ms\n",
      "  5   -9.357111217296       -4.88       -3.78    1.0   75.3ms\n",
      "  6   -9.357111431923       -6.67       -3.85    5.5    148ms\n",
      "  7   -9.357111489773       -7.24       -4.13    1.4   76.3ms\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "self_consistent_field(aluminium_setup(1); tol=1e-4, mixing=SimpleMixing());"
   ],
   "metadata": {},
   "execution_count": 7
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "┌ Warning: Eigensolver not converged\n",
      "│   n_iter =\n",
      "│    8-element Vector{Int64}:\n",
      "│     11\n",
      "│      2\n",
      "│     16\n",
      "│      5\n",
      "│     18\n",
      "│     12\n",
      "│     11\n",
      "│      5\n",
      "└ @ DFTK ~/work/DFTK.jl/DFTK.jl/src/scf/self_consistent_field.jl:80\n",
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime \n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -37.51560153369                   -0.84   10.0    2.26s\n",
      "  2   -37.55281372279       -1.43       -1.56    3.0    730ms\n",
      "  3   -29.38234152432   +    0.91       -0.61   11.0    1.70s\n",
      "  4   -37.08889064507        0.89       -1.21    7.8    1.59s\n",
      "  5   -37.49211129596       -0.39       -1.42    3.2    1.00s\n",
      "  6   -37.54129546608       -1.31       -1.81    2.5    913ms\n",
      "  7   -36.82568168317   +   -0.15       -1.13    6.0    1.75s\n",
      "  8   -37.55866154015       -0.13       -2.11    5.2    1.15s\n",
      "  9   -37.56203429940       -2.47       -2.07    2.8    949ms\n",
      " 10   -37.56334446709       -2.88       -2.35    1.9    805ms\n",
      " 11   -37.56456972761       -2.91       -2.59    1.8    778ms\n",
      " 12   -37.56480497942       -3.63       -2.86    2.9    845ms\n",
      " 13   -37.56491959932       -3.94       -3.17    3.6    958ms\n",
      " 14   -37.56494072591       -4.68       -3.40    3.1    938ms\n",
      " 15   -37.56494016175   +   -6.25       -3.54    4.5    1.09s\n",
      " 16   -37.56494843631       -5.08       -3.93    3.4    1.40s\n",
      " 17   -37.56494963025       -5.92       -4.31    2.2    857ms\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "self_consistent_field(aluminium_setup(4); tol=1e-4, mixing=SimpleMixing());"
   ],
   "metadata": {},
   "execution_count": 8
  },
  {
   "cell_type": "markdown",
   "source": [
    "For completion let us note that the more traditional `mixing=KerkerMixing()`\n",
    "approach would also help in this particular setting to obtain a constant\n",
    "number of SCF iterations for an increasing system size (try it!). In contrast\n",
    "to `LdosMixing`, however, `KerkerMixing` is only suitable to model bulk metallic\n",
    "system (like the case we are considering here). When modelling metallic surfaces\n",
    "or mixtures of metals and insulators, `KerkerMixing` fails, while `LdosMixing`\n",
    "still works well. See the Modelling a gallium arsenide surface example\n",
    "or [^HL2021] for details. Due to the general applicability of `LdosMixing` this\n",
    "method is the default mixing approach in DFTK."
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.4"
  },
  "kernelspec": {
   "name": "julia-1.12",
   "display_name": "Julia 1.12.4",
   "language": "julia"
  }
 },
 "nbformat": 4
}
